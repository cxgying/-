!***************标准规范*******************************************
!GB/T 26978-2011 现场组装立式圆筒平底钢质液化天燃气储罐的设计与建造
!JGJ7-2010 空间网格结构技术规程
!扇形三向网格
!****************预处理*********************************************
finish
/clear
!***************定义参数*******************************************
sectorNum=8!定义扇数
ringNum=14!定义环数
LLLLL=60!定义球形网壳跨度
HHHHH=10!定义球形网壳高度
RR=(HHHHH*HHHHH+LLLLL*LLLLL/4)/(2*HHHHH)!定义球壳半径
ARG=ASIN(LLLLL/(2*RR))*57.29578!定义球壳辐射半角度
EXX=2E11!定义弹性模量
NUXYSS=0.3!定义泊松比
DENSSS=7850!定义密度
!***************前处理*********************************************
/prep7
ET,1,180!指定单元类型为LINK180杆单元
SECTYPE,,LINK
SECDATA,0.002
MP,EX,1,EXX!定义弹性模量
MP,NUXY,1,NUXYSS!定义泊松比
MP,DENS,1,DENSSS!定义密度
*AFUN,DEG!三角函数中用角度量
!***************模型建立*******************************************
*DIM,INNER,ARRAY,((RINGNUM+1)*(RINGNUM+2)/2-1)*SECTORNUM+1,2
INNER(1,1)=0
INNER(1,2)=0
*DO,II,1,RINGNUM,1
*DO,IIIIII,1,SECTORNUM,1
*DO,IIII,1,II,1
INNER(1+SECTORNUM*II*(II-1)/2+II*(IIIIII-1)+IIII,1)=ARG*II/RINGNUM
INNER(1+SECTORNUM*II*(II-1)/2+II*(IIIIII-1)+IIII,2)=(IIII-1)*360/(SECTORNUM*II)+360*(IIIIII-1)/SECTORNUM
*ENDDO
*ENDDO
*ENDDO
K,1,0,0,HHHHH
*DO,ICTRL,2,((RINGNUM+1)*(RINGNUM+2)/2-1)*SECTORNUM+1,1
K,ICTRL,RR*SIN(INNER(ICTRL,1))*COS(INNER(ICTRL,2)),RR*SIN(INNER(ICTRL,1))*SIN(INNER(ICTRL,2)),HHHHH-RR+RR*COS(INNER(ICTRL,1))
*ENDDO

*DO,II,2,SECTORNUM+1,1
L,1,II
*ENDDO

*DO,II,1,RINGNUM-1,1
*DO,IIIIII,1,SECTORNUM,1
L,1+SECTORNUM*II*(II-1)/2+1+II*(IIIIII-1),1+SECTORNUM*II*(II+1)/2+1+(II+1)*(IIIIII-1)
*ENDDO
*ENDDO

*DO,II,1,RINGNUM,1
*DO,IIII,1+SECTORNUM*II*(II-1)/2+1,SECTORNUM*II*(II+1)/2,1
L,IIII,IIII+1
*ENDDO
L,1+SECTORNUM*II*(II-1)/2+1,1+SECTORNUM*II*(II+1)/2
*ENDDO

*DO,II,1,RINGNUM-1,1
*DO,IIIIII,1,SECTORNUM,1
*DO,IIII,1,II,1
L,1+SECTORNUM*II*(II-1)/2+IIII+(IIIIII-1)*II,2+SECTORNUM*II*(II+1)/2+IIII+(IIIIII-1)*(II+1)
L,2+SECTORNUM*II*(II+1)/2+IIII+(IIIIII-1)*(II+1),2+SECTORNUM*II*(II-1)/2+IIII+(IIIIII-1)*II
*ENDDO
*ENDDO
L,1+SECTORNUM*II*(II-1)/2+1,1+SECTORNUM*(II+1)*(II+2)/2
*ENDDO

!*********************************网格划分*************************
!LESIZE, NL1, SIZE, ANGSIZ, NDIV, SPACE, KFORC, LAYER1, LAYER2, KYNDIV
LESIZE,ALL,,,1,,,,,0
LMESH,ALL
FINISH
